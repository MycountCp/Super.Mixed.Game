global function CustomAirAccelVars_Init

const int CUSTOM_AIR_ACCEL_DEFAULT_VALUE = 500

void function CustomAirAccelVars_Init()
{	
	#if MP
		AddPrivateMatchModeSettingArbitrary( "#MODE_SETTING_CATEGORY_PILOT", "custom_air_accel_pilot", CUSTOM_AIR_ACCEL_DEFAULT_VALUE.tostring() ) // 500 is the default airaccel
	#endif
	
	#if SERVER
		AddCallback_OnPlayerRespawned( ApplyCustomPlayerAirAccelOnRespawn )
		AddCallback_OnTitanBecomesPilot( ApplyCustomPlayerAirAccelOnTitanTransfer ) // airaccel is reset after player leaves titan
		AddCallback_OnPilotBecomesTitan( ApplyCustomPlayerAirAccelOnTitanTransfer ) // airaccel is also reset after player enters titan
		AddCallback_OnPlayerGetsNewPilotLoadout( ApplyCustomPlayerAirAccelOnLoadoutChange ) // airaccel is also reset on loadout change for some reason
	#endif
}

#if SERVER
void function ApplyCustomPlayerAirAccelOnRespawn( entity player )
{
	TryApplyCustomPlayerAirAccel( player )
}

void function ApplyCustomPlayerAirAccelOnTitanTransfer( entity player, entity titan )
{
	TryApplyCustomPlayerAirAccel( player )
}

void function ApplyCustomPlayerAirAccelOnLoadoutChange( entity player, PilotLoadoutDef loadout ) 
{
	TryApplyCustomPlayerAirAccel( player )
}

// utility
bool function TryApplyCustomPlayerAirAccel( entity player )
{
	// only apply custom airAccel if playlistvar modified
	if ( !CustomAirAccelerationModified() )
	{
		//print( "custom air acceleration not enabled" )
		return false
	}
	int airAccel = GetCurrentPlaylistVarInt( "custom_air_accel_pilot", int( player.GetPlayerSettingsField( "airAcceleration" ) ) )
	SetPlayerAirAccelerationPermanent( player, airAccel )
	return true
}

bool function CustomAirAccelerationModified()
{
	return GetCurrentPlaylistVarInt( "custom_air_accel_pilot", CUSTOM_AIR_ACCEL_DEFAULT_VALUE ) != CUSTOM_AIR_ACCEL_DEFAULT_VALUE
}
#endif
